plugins {
	id "java"
	id "application"
	id "eclipse"
}

repositories {
	mavenCentral()
	ivy {
		url "https://svn.code.sf.net"
		patternLayout {
			artifact "/p/[organisation]/code/frost/trunk/frost.core/src/main/zipResources/lib/[module]-jdk16-[revision].[ext]"
		}
		metadataSources {
			artifact()
		}
	}
}

tasks.withType(JavaCompile) {
	// Frost-Repair requires Java 8
	// - org.garret.perst.* = old Perst
	// - com.mcobject.perst.* = new Perst (@see Spider)
	//
	// Perst stores the names of used classes in dbs-files.
	// Frost can only read/write dbs-files written with old Perst.
	// Old Perst works with Java = 8.
	// New Perst works with Java >= 8. It can read both "versions" of Perst,
	// but creates files as new Perst such that Frost is unable to read them.
	targetCompatibility = "1.8"

	options.encoding = "UTF-8"
}

application {
	mainClass = "org.frost.repair.Main"
}

dependencies {
	implementation("org.slf4j:slf4j-api:2.0.2")
	implementation("ch.qos.logback:logback-core:1.3.1")
	implementation("ch.qos.logback:logback-classic:1.3.1")
	implementation("jtcfrost:perst:421:jar");
}
